#--- homebrew setup ---#
if [[ -f /opt/homebrew/bin/brew ]]; then
    eval "$(/opt/homebrew/bin/brew shellenv)"
else
    eval "$(/home/linuxbrew/.linuxbrew/bin/brew shellenv)"
fi

#--- history ---#
HISTFILE=~/.zsh_history    # where to save history
HISTSIZE=1000000000        # number of commands to keep in memory during session
SAVEHIST=1000000000        # number of commands to save to disk
HISTORY_IGNORE="(ll|ls|cd|pwd|exit|history|..|...|....)" # patterns to exclude from history
setopt INC_APPEND_HISTORY  # add commands to history immediately, not on shell exit
setopt EXTENDED_HISTORY    # save timestamp and duration with each command
setopt HIST_IGNORE_DUPS    # don't save command if it's same as the previous one
setopt HIST_IGNORE_SPACE   # don't save commands that start with a space
setopt SHARE_HISTORY       # share history between all terminal sessions in real-time
setopt NO_BEEP             # disable terminal beep sound

#--- completions ---#
autoload -Uz compinit && compinit

#--- rustup environment ---#
if command -v rustup &> /dev/null; then
    rustup_prefix="$(brew --prefix rustup 2>/dev/null)"
    if [[ -n "$rustup_prefix" && -d "$rustup_prefix/bin" ]]; then
        export PATH="$rustup_prefix/bin:$PATH"
    fi
    unset rustup_prefix
fi

#--- java environment ---#
if [[ -d "/Library/Java/JavaVirtualMachines/temurin-25.jdk/Contents/Home" ]]; then
    export JAVA_HOME=/Library/Java/JavaVirtualMachines/temurin-25.jdk/Contents/Home
    export PATH=$JAVA_HOME/bin:$PATH
fi

#--- AI assistants ---#
export PATH="$HOME/source/dotfiles/claude:$PATH"
export PATH="$HOME/.local/bin:$PATH" # agent (cursor)

#--- shell tools ---#
alias fd="fd --hidden --no-ignore --exclude cdk.out --exclude node_modules --exclude .git --exclude __pycache__ --exclude .venv"
if [[ -x "$HOME/.local/bin/starship" ]]; then
    eval "$($HOME/.local/bin/starship init zsh)"
elif command -v starship &> /dev/null; then
    eval "$(starship init zsh)"
fi

if command -v zoxide &> /dev/null; then
    eval "$(zoxide init zsh)"
fi

if command -v atuin &> /dev/null; then
    eval "$(atuin init zsh --disable-up-arrow)"
fi

#--- wsl ---#
if [[ -f /proc/sys/fs/binfmt_misc/WSLInterop ]]; then
    export BROWSER=wslview
fi

#--- aliases ---#
if command -v eza &> /dev/null; then
    alias ls="eza"
    alias ll="eza -lh"
    alias la="eza -lah"
else
    alias ll="ls -lh"
    alias la="ls -lah"
fi
alias ..="cd .."
alias y="yt-dlp"
alias lg="lazygit"

# Claude Code wrapper:
# - Builds ~/.claude/CLAUDE.md from ~/source/dotfiles/claude/CLAUDE.base.md + ~/.claude/CLAUDE.local.md
# - Loads MCP config from ~/.claude/mcp.local.json if present
claude() {
    mkdir -p ~/.claude
    cat ~/source/dotfiles/claude/CLAUDE.base.md > ~/.claude/CLAUDE.md
    echo >> ~/.claude/CLAUDE.md
    cat ~/.claude/CLAUDE.local.md >> ~/.claude/CLAUDE.md 2>/dev/null
    local mcp_args=()
    [[ -f ~/.claude/mcp.local.json ]] && mcp_args=(--mcp-config ~/.claude/mcp.local.json)
    npx @anthropic-ai/claude-code "${mcp_args[@]}" "$@"
}

# Cursor Agent CLI wrapper:
# - Cursor Agent reads MCP config from .cursor/mcp.json or ~/.cursor/mcp.json
# - Keep Claude + Cursor in sync (Claude is often the superset)
agent() {
    mkdir -p ~/.cursor
    local claude_mcp="$HOME/.claude/mcp.local.json"
    local cursor_mcp="$HOME/.cursor/mcp.json"

    if [[ -f "$claude_mcp" ]]; then
        # If Cursor has no config, just reuse Claude's.
        if [[ ! -e "$cursor_mcp" ]]; then
            ln -s "$claude_mcp" "$cursor_mcp" 2>/dev/null || cp "$claude_mcp" "$cursor_mcp"
        # If Cursor has its own config file, merge in any missing MCP servers from Claude.
        elif [[ ! -L "$cursor_mcp" ]]; then
            python3 - "$cursor_mcp" "$claude_mcp" <<'PY' >/dev/null 2>&1
import json
import shutil
import sys
import time
from pathlib import Path

cursor_path = Path(sys.argv[1]).expanduser()
claude_path = Path(sys.argv[2]).expanduser()

try:
    cursor = json.loads(cursor_path.read_text())
except Exception:
    cursor = {}

claude = json.loads(claude_path.read_text())

cursor_servers = cursor.get("mcpServers") or {}
claude_servers = claude.get("mcpServers") or {}

# Cursor entries win on conflicts (lets you override tokens/settings there)
merged_servers = dict(claude_servers)
merged_servers.update(cursor_servers)

merged = dict(cursor)
merged["mcpServers"] = merged_servers

if merged != cursor:
    backup = cursor_path.with_name(cursor_path.name + f".bak.{int(time.time())}")
    shutil.copy2(cursor_path, backup)
    cursor_path.write_text(json.dumps(merged, indent=2, sort_keys=False) + "\n")
PY
        fi
    fi
    command agent "$@"
}
pypi() { curl -sS "https://pypi.org/pypi/$1/json" | jq -r .info.version; }

#--- fix key bindings ---#
bindkey "^[[H" beginning-of-line     # Home key
bindkey "^[[F" end-of-line           # End key
bindkey "^[OH" beginning-of-line     # Home key (alternative)
bindkey "^[OF" end-of-line           # End key (alternative)
bindkey "^[[1~" beginning-of-line    # Home key (another variant)
bindkey "^[[4~" end-of-line          # End key (another variant)
bindkey "^[[3~" delete-char          # Delete key

#--- local configuration ---#
[[ -f ~/.zshrc.local ]] && . ~/.zshrc.local
